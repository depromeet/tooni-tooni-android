plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

kapt {
    useBuildCache = true
}

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildToolVersion

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def versionBuild = 0

    defaultConfig {
        applicationId "kr.tooni.tooni"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "BASE_URL", getBaseUrl("BASE_URL")
        project.archivesBaseName = "android-" + defaultConfig.versionCode
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        debug {
            resValue "string", "app_name", "Tooni Tooni_디버그"
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            firebaseCrashlytics {
                mappingFileUploadEnabled false
            }
            ext.enableCrashlytics = false
        }
        release {
            resValue "string", "app_name", "Tooni Tooni"
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    bundle {
        abi.enableSplit = true
        density.enableSplit = true
        language.enableSplit = false
    }
}

dependencies {

    implementation deps.kotlin.stdlib
    implementation deps.coroutines.core
    implementation deps.coroutines.android

    implementation deps.jetpack.appCompat
    implementation deps.jetpack.coreKtx
    implementation deps.jetpack.recyclerView
    implementation deps.jetpack.design
    implementation deps.jetpack.cardView
    implementation deps.jetpack.annotations
    implementation deps.jetpack.constraintLayout
    implementation deps.jetpack.lifecycle.liveData
    implementation deps.jetpack.lifecycle.viewModel
    implementation deps.jetpack.lifecycle.savedState
    kapt deps.jetpack.lifecycle.compiler

    implementation deps.androidx.activityKtx
    implementation deps.androidx.fragmentKtx

    implementation deps.glide.core
    implementation deps.glide.okhttp
    implementation deps.glide.transformations
    kapt deps.glide.compiler

    implementation deps.rx.core
    implementation deps.rx.android
    implementation deps.rx.kotlin
    implementation deps.rx.relay

    implementation deps.room.runtime
    implementation deps.room.ktx
    kapt deps.room.compiler

    implementation "com.google.dagger:hilt-android:2.36"
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03'
    kapt "com.google.dagger:hilt-android-compiler:2.36"
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    implementation deps.gson
    implementation deps.retrofit.core
    implementation deps.retrofit.gson
    implementation deps.retrofit.rxjava
    implementation deps.okhttp.logging

    implementation platform(deps.firebase.bom)
    implementation deps.firebase.crashlyticsKtx
    implementation deps.firebase.analyticsKtx
    implementation deps.firebase.authKtx

    implementation deps.timber

    releaseImplementation deps.chuckNoOp
    debugImplementation deps.chuck
    debugImplementation deps.leakCanary

    androidTestImplementation deps.test.runner
    androidTestImplementation deps.test.rules
    androidTestImplementation deps.test.junitext
    testImplementation deps.test.junit
    testImplementation deps.test.kotlin
    testImplementation deps.test.kotlinJunit
    testImplementation deps.test.kotlinMockito
    testImplementation deps.test.core
    testImplementation deps.test.archCore
    testImplementation deps.test.robolectric
}

String getBaseUrl(String name) {
    try {
        def properties = new Properties()
        properties.load(new FileInputStream(rootProject.file("./local.properties")))
        def baseUrl = properties.getProperty(name)
        if (baseUrl != null) baseUrl else "\"https://fake.api.com\""
    } catch (FileNotFoundException e) {
        e.printStackTrace()
        "\"https://fake.api.com\""
    }
}
